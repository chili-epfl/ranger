<!DOCTYPE aesl-source>
<network>


<!--list of global events-->
<event size="16" name="mainFeedback"/>
<event size="20" name="mainFeedbackWithEncoders"/>
<event size="4" name="setLed"/>
<event size="10" name="receiverFeedback"/>
<event size="2" name="setSpeed"/>
<event size="1" name="enableEncoders"/>
<event size="2" name="playLedVid"/>
<event size="0" name="stopLedVid"/>
<event size="4" name="neuilFeedback"/>
<event size="8" name="neuilEvent"/>
<event size="10" name="customEvent"/>
<event size="7" name="emitterEvent"/>
<event size="1" name="enableFeedback"/>
<event size="1" name="openEyelids"/>
<event size="3" name="EyeCartesianSet"/>
<event size="3" name="EyePolarSet"/>
<event size="2" name="openEyelid"/>
<event size="4" name="setLedsCol"/>
<event size="3" name="setAllLeds"/>
<event size="0" name="turnOffLeds"/>
<event size="4" name="setLedsRow"/>


<!--list of constants-->
<constant value="600" name="CURRENT_LIMIT"/>
<constant value="60" name="POS_REACHED_STEPS"/>
<constant value="13" name="NB_TIMER0_CYCLES"/>
<constant value="6" name="RF_DATA_LAST_INDEX"/>
<constant value="10" name="RANGER_FIRST_ID"/>
<constant value="14" name="RANGER_LAST_ID"/>
<constant value="15" name="CHARGINGSTATION_FIRST_ID"/>
<constant value="19" name="CHARGINGSTATION_LAST_ID"/>
<constant value="20" name="BEACON_FIRST_ID"/>
<constant value="21" name="BEACON_LAST_ID"/>
<constant value="0" name="NO_ID"/>
<constant value="255" name="BROADCAST_ID"/>
<constant value="0" name="DEST_ID_POS"/>
<constant value="1" name="PACKET_ID_POS"/>
<constant value="2" name="DIST_POS"/>
<constant value="3" name="ANGLE_POS"/>
<constant value="4" name="LOLETTES_POS"/>


<!--show keywords state-->
<keywords flag="true"/>


<!--node RangerMain-->
<node nodeId="1" name="RangerMain">var touchBuffer[2]
var i
var j
var timer = 0
var encoderEnabled = 0
var playingVid = 0
var repeatVid = 0
var TouchRight
var TouchLeft
var TouchRear
var Bumper = 0
var mainFeedbackEnable = 0

mot1.pid.current_max = CURRENT_LIMIT
mot2.pid.current_max = CURRENT_LIMIT

# switch off all leds
for i in 0:185 do
	call led.set(i, 0, 0, 0)
end

touch._disable = 0

# stop motors
mot1.pid.enable = 1
mot1.pid.target_current = 0
mot1.pid.target_speed = 0
mot2.pid.enable = 1
mot2.pid.target_current = 0
mot2.pid.target_speed = 0

onevent setLed
  call led.set(event.args[0], event.args[1], event.args[2], event.args[3])

onevent setLedsCol
# 31 cols in total, including corners
if event.args[0] >= 0 and event.args[0] &lt;= 30 then
	for i in 0:5 do
		call led.set(event.args[0] * 6 + i, event.args[1], event.args[2], event.args[3])		
	end
end

onevent setLedsRow
# 6 rows in total 
if event.args[0] >= 0 and event.args[0] &lt;= 5 then
	for i in 0:30 do
		if i % 2 == 0 and i != 0 and i != 10 and i != 20 and i != 30 then
			call led.set(event.args[0] + i * 6, event.args[1], event.args[2], event.args[3])
		else	
			call led.set((i+1) * 6 - event.args[0], event.args[1], event.args[2], event.args[3])
		end
	end
end

onevent setAllLeds
for i in 0:185 do
  call led.set(i, event.args[0], event.args[1], event.args[2])
end

onevent turnOffLeds
for i in 0:185 do
  call led.set(i, 0, 0, 0)
end


# Bumper computed either via the physical bumper or with the motor current
sub computeBumper
if bumper == 1 or mot1.pid.target_current > (CURRENT_LIMIT - 20) or mot1.pid.target_current &lt; -(CURRENT_LIMIT - 20) or mot2.pid.target_current > (CURRENT_LIMIT - 20) or mot2.pid.target_current &lt; -(CURRENT_LIMIT - 20) then
	Bumper = 1
else
	Bumper = 0
end

# Compress touchSensors in order to use only one variable by pannel
sub touchCompress
TouchRight = 0
TouchLeft = 0
TouchRear = 0
j = 1
for i in 0:8 do
	TouchRight += touch.right[i]*j
	TouchLeft  += touch.left[i]*j
#	TouchRear  += touch.rear[i]*j
	TouchRear = 0	## For the moment this touch panel is not available !!
	j *= 2
end


#event acc used to send data
onevent acc
if mainFeedbackEnable != 0 then
	callsub touchCompress
	callsub computeBumper
	if encoderEnabled == 1 then
		emit mainFeedbackWithEncoders[acc, sharp, vbat, Bumper, scales.median, mot1.speed, mot2.speed, TouchRight, TouchLeft, TouchRear, mot1.enc._pulse, mot2.enc._pulse, _status.ExtPower, mot1._raw_current, -mot2._raw_current]
	else
		emit mainFeedback[acc, sharp, vbat, Bumper, scales.median, mot1.speed, mot2.speed, TouchRight, TouchLeft, TouchRear, _status.ExtPower, mot1._raw_current, -mot2._raw_current]
	end
end
#if _status.ExtPower == 1 then
#	call led.vid.stop()
#end


onevent setSpeed
if event.args[0] == 0 and event.args[1] == 0 then
	mot1.pid.target_speed = 0
	mot1.pid.enable = 1
	mot1.pid.target_current = 0
	mot2.pid.target_speed = 0
	mot2.pid.enable = 1
	mot2.pid.target_current = 0
else
	mot1.pid.enable = 2
	mot1.pid.target_speed = event.args[0]
	mot2.pid.enable = 2
	mot2.pid.target_speed = event.args[1]
end

onevent enableEncoders
encoderEnabled = event.args[0]

onevent playLedVid
repeatVid = 0
call led.vid.stop()
playingVid = event.args[0]
repeatVid = event.args[1]
call led.vid.start(playingVid)

onevent led # Video is finished
if repeatVid == 1 then
	call led.vid.start(playingVid)
else
	call led.vid.stop()
end

onevent stopLedVid
call led.vid.stop()
repeatVid = 0

onevent enableFeedback
mainFeedbackEnable = event.args[0]
</node>


<!--node rab2-->
<node nodeId="7" name="rab2">var rabFeedbackEnable = 0
var c
var data_buffer[RF_DATA_LAST_INDEX +1]
# Data frame:
# 0: dest rf id (0: No dest, 255: broadcast)
# 1: packet id
# 2: distance of detected RAB (dest rf id)
# 3: angle of detected RAB (dest rf id)
# 4: lolettes stat [1|2|4] or others
# 5: ...
# 6: ...

rf.swarm_size = 25
rf.slot_time = 4000
rx.power.en = 1
tx.power.en = 1

data_buffer[DEST_ID_POS] = NO_ID  # nothing to transmit for the moment
rf.tx_data[PACKET_ID_POS] = 0

call rf.start()


onevent packet.tx
rf.tx_data[DEST_ID_POS] = NO_ID  # nothing to transmit for the moment; data are not ready
if (data_buffer[DEST_ID_POS] != 0) then  # transmission required
	for c in 1:RF_DATA_LAST_INDEX do
		rf.tx_data[c] = data_buffer[c]
	end
	rf.tx_data[DEST_ID_POS] = data_buffer[DEST_ID_POS]
	data_buffer[DEST_ID_POS] = NO_ID  # data processed
else
	rf.tx_data[PACKET_ID_POS] += 1  # to see the timeslot
end

onevent packet.rx
if (rabFeedbackEnable != 0) then
# Manage only the Charging stations or Beacons data
	if (rx.data[DEST_ID_POS] == rf.id) or (rx.data[DEST_ID_POS] == BROADCAST_ID) then
		if ((rx.source >= CHARGINGSTATION_FIRST_ID) and (rx.source &lt;= CHARGINGSTATION_LAST_ID))
		or ((rx.source >= BEACON_FIRST_ID) and (rx.source &lt;= BEACON_LAST_ID)) then
			emit receiverFeedback[rx.source, rx.angle, rx.dist, rx.data]
		end
	end
end

onevent emitterEvent
for c in 0:RF_DATA_LAST_INDEX do
	data_buffer[c] = event.args[c]
end

onevent enableFeedback
rabFeedbackEnable = event.args[0]</node>


<!--node neuil-->
<node nodeId="15" name="neuil">var mainFeedbackEnable = 0

# Just for Neuil init in order to move the pupils slowly; the magnets can be "englued" and lossed
var initNeuil = 1
var a = 0
var da = 4
var Rmax = 360
var dR = 4
var R = 1
var i

timer[0] = 50
timer[1] = 100

sharp.enable[0] = 1
sharp.enable[1] = 1
sharp.enable[2] = 1

for i in 0:3 do
	call eyelid.set (i,100)
end

onevent timer0
if initNeuil == 1 then
	if R > Rmax then
		R = 0
		dR = 0
		initNeuil = 0
	end
	call eye.polar_set(0,R,a)
	call eye.polar_set(1,R,360-a)

	for i in 0:3 do
		call eyelid.set (i,100-R%180)
	end
		
	R+=dR
	a+=da
	if a &lt; 0 then
		a = 360
	else
		a%=360
	end
end

# feedback 
onevent timer1
if mainFeedbackEnable != 0 then
	emit neuilFeedback [sharp.value, lolette]
end

onevent enableFeedback
mainFeedbackEnable = event.args[0]

onevent openEyelid
call eyelid.set (event.args[0],event.args[1])

onevent openEyelids
for i in 0:3 do
	call eyelid.set (i,event.args[0])
end

onevent EyeCartesianSet
call eye.cartesian_set(event.args[0],event.args[1],event.args[2])

onevent EyePolarSet
call eye.polar_set(event.args[0],event.args[1],event.args[2])</node>


</network>
